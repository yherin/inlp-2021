!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Multichar_Symbols

+Noun                ! POS
+Verb                ! POS

!! LEXICON Verb-Inflection-I

+Infinitive-A
+1Person_Singular
+2Person_Singular
+1Person_Plural
+2Person_Plural
+3Person_Plural

!! LEXICON Noun-Inflection-I

+Nominative
+Genitive
+Inessive
+Adessive
+Allative
+Elative
+Ablative
+Essive
+Translative
+Illative

!! %>                        !! morpheme boundary (Apertium)
!! +                         !! morpheme boundary

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Root

Noun-stems ;
Verb-stems ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Clitics

# ;
+Interrogative:⛒ko   # ;
!+Emphatic:⛒kin   # ;
!+Negative:⛒kaan   # ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Verb-Inflection-I

+Infinitive-A:%>a     Clitics ;
+1Person_Singular:%>n     Clitics ;
+2Person_Singular:%>t     Clitics ;
+1Person_Plural:%>mme   Clitics ;
+2Person_Plural:%>tte   Clitics ;
+3Person_Plural:%>vat   Clitics ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Noun-Inflection-I

!! OK too: 
!! +Nominative:    # ; !!                       

+Nominative:%>0    Clitics ; !!                    
+Genitive:%>n      Clitics ; !! of                 
+Inessive:%>ssa    Clitics ; !! in                 
+Adessive:%>lla    Clitics ; !! at                 
+Allative:%>lle    Clitics ; !! onto               
+Elative:%>sta     Clitics ; !! out-of from-within 
+Ablative:%>lta    Clitics ; !! from               
+Essive:%>na       Clitics ; !! as                 
+Translative:%>ksi Clitics ; !! as-into            
+Illative:%>Vn     Clitics ; !! into               


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Noun-stems

talo+Noun:talo    Noun-Inflection-I ;   ! (house)
pala+Noun:pala    Noun-Inflection-I ;   ! (piece)
savu+Noun:savu    Noun-Inflection-I ;   ! (smoke)
ovi+Noun:ovI      Noun-Inflection-I ;   ! (door) !! oveko
katto+Noun:katto  Noun-Inflection-I ;   ! (roof)

isä+Noun:isä      Noun-Inflection-I ;  ! (father)
hissi+Noun:hissi  Noun-Inflection-I ;  ! (lift)
kivi+Noun:kivI    Noun-Inflection-I ;  ! (stone)
pöytä+Noun:pöytä  Noun-Inflection-I ;  ! (table)

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Verb-stems

palaa+Verb:pala      Verb-Inflection-I ;   ! (burn)
ajaa+Verb:aja        Verb-Inflection-I ;   ! (drive)
!! nukkua+Verb:nukku Verb-Inflection-I ;   ! (sleep)

kysyä+Verb:kysy      Verb-Inflection-I ;  ! (ask)
kestää+Verb:kestä    Verb-Inflection-I ;  ! (last)
heittää+Verb:heittä  Verb-Inflection-I ;  ! (berate)
löytää+Verb:löytä    Verb-Inflection-I ;  ! (find)
