!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Multichar_Symbols

+Noun                ! POS
+Verb                ! POS

!! LEXICON Verb-Infl-I

+InfA
+1_Sg
+2_Sg
+1_Pl
+2_Pl
+3_Pl

!! LEXICON Noun-Infl-I

+Nom
+Gen
+Ine
+Ade
+All
+Ela
+Abl
+Ess
+Tra

!! %>                        !! morpheme boundary (Apertium)
!! +                         !! morpheme boundary

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Root

Noun-stems ;
Verb-stems ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Verb-Infl-I

+InfA:%>a # ;
+1_Sg:%>n # ;
+2_Sg:%>t # ;
+1_Pl:%>mme # ;
+2_Pl:%>tte # ;
+3_Pl:%>vat # ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Noun-Infl-I

!! OK too: 
!! +Nom:    # ; !!                       Nominative

+Nom:%>0    # ; !!                       Nominative
+Gen:%>n    # ; !! of                 !! Genitive
+Ine:%>ssa  # ; !! in                 !! Inessive 
+Ade:%>lla  # ; !! at                 !! Adessive 
+All:%>lle  # ; !! onto               !! Allative 
+Ela:%>sta  # ; !! out-of from-within !! Elative
+Abl:%>lta  # ; !! from               !! Ablative 
+Ess:%>na   # ; !! as                 !! Essive
+Tra:%>ksi  # ; !! as-into            !! Translative
+Ill:%>Vn   # ; !! into               !! Illative


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Noun-stems

talo+Noun:talo    Noun-Infl-I ;   ! (house)
pala+Noun:pala    Noun-Infl-I ;   ! (piece)
savu+Noun:savu    Noun-Infl-I ;   ! (smoke)
ovi+Noun:ovI      Noun-Infl-I ;   ! (door)
katto+Noun:katto  Noun-Infl-I ;   ! (roof)

isä+Noun:isä      Noun-Infl-I ;  ! (father)
hissi+Noun:hissi  Noun-Infl-I ;  ! (lift)
kivi+Noun:kivI    Noun-Infl-I ;  ! (stone)

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Verb-stems

palaa+Verb:pala      Verb-Infl-I ;   ! (burn)
ajaa+Verb:aja        Verb-Infl-I ;   ! (drive)
!! nukkua+Verb:nukku Verb-Infl-I ;   ! (sleep)

kysyä+Verb:kysy      Verb-Infl-I ;  ! (ask)
kestää+Verb:kestä    Verb-Infl-I ;  ! (last)
heittää+Verb:heittä  Verb-Infl-I ;  ! (berate)
